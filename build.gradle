buildscript {
    apply from: "gradle/versions.gradle"
    apply from: gradle.extended.files.modules

    dependencies {
        classpath additionals.nexus.gradlePlugin
        classpath additionals.sonarqube.gradlePlugin
        classpath additionals.license.gradlePlugin
    }
}

plugins {
    alias(additionals.plugins.dokka)
    alias(additionals.plugins.publish.nexus)
    alias(additionals.plugins.kotlin.multiplatform) apply false
    alias(additionals.plugins.android.library) apply false
    alias(additionals.plugins.kotlin.jvm) apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }
}

group = ext.groupId
version = ext.version

subprojects {
    group = rootProject.ext.groupId
    version = rootProject.ext.version

    task allDeps(type: DependencyReportTask) {}

    if (isSourcesModule(it)) {
        apply from: gradle.extended.files.moduleSetup
    }
}

/**
 * Definitions for the publication only
 */
def getExtraString = { String name ->
    if (rootProject.ext.has(name)) return rootProject.ext[name]?.toString()
    else return null
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = URI.create("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = URI.create("https://central.sonatype.com/repository/maven-snapshots/")
            username = getExtraString("sonatypeUsername") ?: ""
            password = getExtraString("sonatypePassword") ?: ""
        }
    }
}

tasks.register("quality") { qualityTask ->
    group = "verification"

    subprojects.each { project ->
        def tasks = ["ktlint", "detekt"].collect {
            project.tasks.named(it)
        }.findAll { it != null }

        qualityTask.dependsOn(tasks)
    }
}